plugins {
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id 'com.google.cloud.tools.jib' version '3.3.0'
    id 'java'
}

group = 'eu.flexiverse.spannerdemos.spannerjpa'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

jib {
    to {
        image = 'gcr.io/${PROJECT_NAME}/spannerdemos/spannerjpa'

        project.afterEvaluate {
            ArrayList<String> tmpTags = new ArrayList<String>()
            tmpTags.add(project.version.toString())

            String tagName = "$System.env.TAG_NAME"
            if ((tagName != null) && (!"null".equalsIgnoreCase(tagName)) && !tagName.isEmpty()) {
                tmpTags.add(tagName)
                tmpTags.add('latest')
                tmpTags.add('prod-latest')
            }

            String tagShortSha = "$System.env.SHORT_SHA"
            if ((tagShortSha != null) && (!"null".equalsIgnoreCase(tagShortSha)) && !tagShortSha.isEmpty()) {
                tmpTags.add(tagShortSha)
                //tags = [project.provider{[version]}, 'latest']
            }

            String tagBranchName = "$System.env.BRANCH_NAME"
            if ((tagBranchName != null) && (!"null".equalsIgnoreCase(tagBranchName)) && ("develop".equalsIgnoreCase( tagBranchName ))) {
                tmpTags.add('dev')
            }

            tags = tmpTags
            println "Applying tags: "
            println tags
        }
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudGcpVersion', "3.3.0")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'com.google.cloud:spring-cloud-gcp-starter'
    implementation 'com.google.cloud:spring-cloud-gcp-starter-logging'

    implementation 'com.google.cloud:google-cloud-spanner-hibernate-dialect:1.5.4'
    implementation 'com.google.cloud:google-cloud-spanner-jdbc:2.7.7'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "com.google.cloud:spring-cloud-gcp-dependencies:${springCloudGcpVersion}"
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}